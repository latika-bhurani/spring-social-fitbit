buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:docbook-reference-plugin:0.2.8'
        classpath 'me.champeau.gradle:gradle-javadoc-hotfix-plugin:0.1'
        classpath('org.asciidoctor:asciidoctor-gradle-plugin:0.7.0')
        classpath('org.asciidoctor:asciidoctor-java-integration:0.1.4.preview.1')
        classpath 'io.spring.gradle:spring-io-plugin:0.0.4.RELEASE'
    }
}

// Set up different subproject lists for individual configuration
ext.javaProjects = subprojects.findAll { project -> project.name != 'docs' && project.name != 'manual' }
//ext.docProjects = subprojects.findAll { project -> project.name == 'docs' || project.name == 'manual' }
ext.coreModuleProjects = javaProjects

configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'javadocHotfix'

    group = 'org.springframework.social'

    sourceCompatibility=1.7
    targetCompatibility=1.7

    [compileJava]*.options*.compilerArgs = ['-Xlint:none']

    //sourceSets.test.resources.srcDirs = ['src/test/resources', 'src/test/java']

    //tasks.withType(Test).all {
    //    systemProperty("java.awt.headless", "true")
    //}

    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }

    //dependencies {
    //    testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
    //    testCompile "junit:junit-dep:$junitVersion"
    //    testCompile "org.mockito:mockito-all:$mockitoVersion"
    //    testCompile "org.springframework:spring-test:$springVersion"
    //}

    ext.javadocLinks = [
        "http://docs.oracle.com/javase/7/docs/api/",
        "http://docs.oracle.com/javaee/7/api/",
        "http://docs.spring.io/spring/docs/${springVersion}/javadoc-api/",
        "http://docs.jboss.org/jbossas/javadoc/4.0.5/connector/"
    ] as String[]

    // servlet-api (2.5) and tomcat-servlet-api (3.0) classpath entries should not be
    // exported to dependent projects in Eclipse to avoid false compilation errors due
    // to changing APIs across these versions
    eclipse.classpath.file.whenMerged { classpath ->
        classpath.entries.findAll { entry -> entry.path.contains('servlet-api') }*.exported = false

        // When the root project's name is the same as a dependency project's name, the name ends
        // up being duplicated in the classpath entry. For example a dependency on "spring-social-fitbit"
        // would have a path of "/spring-social-fitbit-spring-social-fitbit".
        // The following lines set the path properly in that case.
        classpath.entries.findAll { entry -> 
            if (entry.path.equals("/" + rootProject.name + "-" + rootProject.name)) {
                entry.path = "/" + rootProject.name;
            }
        }
    }
}

configure(subprojects) { subproject ->
    apply from: "${rootProject.projectDir}/publish-maven.gradle"

    if (project.hasProperty('platformVersion')) {
        apply plugin: 'spring-io'

        repositories {
            maven { url "https://repo.spring.io/libs-snapshot" }
        }

        //dependencyManagement {
        //    springIoTestRuntime {
        //        imports {
        //            mavenBom "io.spring.platform:platform-bom:${platformVersion}"
        //        }
        //    }
        //}
    }

    jar {
        manifest.attributes['Implementation-Title'] = subproject.name
        manifest.attributes['Implementation-Version'] = subproject.version

        from("${rootProject.projectDir}/src/dist") {
            include "license.txt"
            include "notice.txt"
            into "META-INF"
            expand(copyright: new Date().format('yyyy'), version: project.version)
        }
    }

    javadoc {
        description = "Generates project-level javadoc for use in -javadoc jar"

        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.links(project.ext.javadocLinks)
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        //options.overview = "${projectDir}/src/main/java/overview.html"
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

}


project('spring-social-fitbit') {
    description = 'Fitbit API'
    dependencies {
        compile ("org.springframework.social:spring-social-core:$springSocialVersion")
        compile ("org.springframework.social:spring-social-config:$springSocialVersion")
        compile ("org.springframework.social:spring-social-security:$springSocialVersion", optional)
        compile ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
        compile ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
        compile ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
        compile ("javax.servlet:javax.servlet-api:$servletApiVersion", provided)
        testCompile ("org.springframework:spring-test:$springVersion")
    }
}

configure(rootProject) {
    description = 'Spring Social Fitbit'

    // don't publish the default jar for the root project
    configurations.archives.artifacts.clear()

    dependencies { // for integration tests
    }
    
    task api(type: Javadoc) {
        group = "Documentation"
        description = "Generates aggregated Javadoc API documentation."
        title = "${rootProject.description} ${version} API"
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = rootProject.description
        options.overview = "src/api/overview.html"
        options.links(project.ext.javadocLinks)
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }

        source subprojects.collect { project ->
            project.sourceSets.main.allJava
        }

        classpath = files(subprojects.collect { project ->
            project.sourceSets.main.compileClasspath
        })

        maxMemory = "1024m"
        destinationDir = new File(buildDir, "api")
    }

    // Task for creating the distro zip

    task dist(type: Zip) {
        dependsOn subprojects*.tasks*.matching { task -> task.name == 'assemble' || task.name.endsWith('Zip') || task.name.endsWith('generatePom') }
        classifier = 'dist'

        //evaluationDependsOn(':docs')
        //evaluationDependsOn(':docs:manual')

        def zipRootDir = "${project.name}-$version"
        into(zipRootDir) {
            from(rootDir) {
                include '*.txt'
            }
            //into('docs') {
            //    with(project(':docs').apiSpec)
            //    with(project(':docs:manual').spec)
            //}
            into('dist') {
                from coreModuleProjects.collect {project -> project.libsDir }
            }
        }
    }

    artifacts {
        archives dist
        //archives project(':docs').docsZip
        //archives project(':docs').schemaZip
    }    

    task wrapper(type: Wrapper) {
        description = 'Generates gradlew[.bat] scripts'
        gradleVersion = '1.11'
    }

}

//configure(docProjects) {
//    tasks.findByPath("artifactoryPublish")?.enabled = false
//}

task updateRootDocs << {
    copy {
        from 'src/dist'
        into "${rootProject.projectDir}"
        include 'notice.txt'
        expand(copyright: new Date().format('yyyy'), version: project.version)
        rename { filename -> 'NOTICE' }
    }

    copy {
        from 'src/dist'
        into "${rootProject.projectDir}"
        include 'license.txt'
        rename { filename -> 'LICENSE' }
    }
}

build.dependsOn('updateRootDocs')
